# Maintainer: Phillip Dixon <phil@dixon.gen.nz>
pkgname=c720-modules
pkgver=3.14.1
pkgrel=2
pkgdesc="Build patched modules to support acer c720."
arch=('i686' 'x86_64')
url="www.kernel.org"
depends=('linux>=3.14' 'linux<3.15')
makedepends=('linux-headers>=3.14' 'linux-headers<3.15')
license=('GPL2')
options=(!strip)
install=c720-modules.install

source=("https://kernel.org/pub/linux/kernel/v3.x/linux-3.14.1.tar.xz"
        '01-Add-Haswell-ULT-device-IDs.patch'
        '05-Add-names-of-Haswell-ULT-i2c-busses.patch'
        '06-Add-HP-Chromebook-14.patch'
        '07-Add-Acer-C720.patch'
       )
md5sums=('f53082540eed294f486a43fc67646ed2'
         'bb90cf3028f16c6bd4c7904fd52ce5b9'
         '4bcc01c1566258852fc3c155403e119d'
         'c2384f642b678168e9cfae62a1ff0536'
         '18073a49be90beb2fb34cf7dd020425b')
_archkernver=${pkgver}-1-ARCH
_kernver=3.14.1
prepare() {
  cd linux-${_kernver}

  patch -p1 -i "$srcdir/01-Add-Haswell-ULT-device-IDs.patch"
  patch -p1 -i "$srcdir/05-Add-names-of-Haswell-ULT-i2c-busses.patch"
  patch -p1 -i "$srcdir/06-Add-HP-Chromebook-14.patch"
  patch -p1 -i "$srcdir/07-Add-Acer-C720.patch"

  cp /usr/lib/modules/${_archkernver}/build/Module.symvers .

  zcat /proc/config.gz > ./.config
  make oldconfig

  make prepare
  make modules_prepare
}

build() {
  cd linux-${_kernver}

  make SUBDIRS=drivers/platform/chrome modules
  make SUBDIRS=drivers/i2c/busses modules
}

package() {
  install -D -m644 "${srcdir}/linux-${_kernver}/drivers/i2c/busses/i2c-designware-core.ko" \
          "${pkgdir}/usr/lib/modules/${_archkernver}/updates/i2c-designware-core.ko"
  install -D -m644 "${srcdir}/linux-${_kernver}/drivers/i2c/busses/i2c-designware-pci.ko" \
          "${pkgdir}/usr/lib/modules/${_archkernver}/updates/i2c-designware-pci.ko"
  install -D -m644 "$srcdir/linux-${_kernver}/drivers/platform/chrome/chromeos_laptop.ko" \
          "${pkgdir}/usr/lib/modules/${_archkernver}/updates/chromeos_laptop.ko"
  gzip "${pkgdir}/usr/lib/modules/${_archkernver}/updates/"*.ko
  #sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/c720-modules.install"
}
